apply plugin: 'com.android.library'

group = 'com.davemorrissey.labs'
archivesBaseName = 'subsampling-scale-image-view'
version = '3.10.1'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 28
        consumerProguardFiles 'proguard-rules.txt'
    }
}

configurations {
    javadocs
}

dependencies {
    javadocs 'androidx.exifinterface:exifinterface:1.0.0'
    javadocs 'androidx.annotation:annotation:1.1.0-alpha01'
    implementation 'androidx.annotation:annotation:1.1.0-alpha01'
    implementation 'androidx.exifinterface:exifinterface:1.0.0'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    destinationDir = rootProject.file("docs/javadoc")
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    classpath += configurations.javadocs
    options.noTimestamp = true
    options.links = ['https://developer.android.com/reference/']
}

apply from: project.file('../release.gradle')
//apply from: rootProject.file('release.gradle')
