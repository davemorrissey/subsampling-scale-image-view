apply plugin: 'com.android.library'

group = 'com.davemorrissey.labs'
archivesBaseName = 'subsampling-scale-image-view'
version = '3.10.1'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        consumerProguardFiles 'proguard-rules.txt'
    }
}

configurations {
    javadocs
}

dependencies {
    javadocs 'com.android.support:support-annotations:27.1.1'
    javadocs 'com.android.support:exifinterface:27.1.1'
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'com.android.support:exifinterface:27.1.1'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    destinationDir = rootProject.file("docs/javadoc")
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
    classpath += configurations.javadocs
    options.noTimestamp = true
    options.links = ['https://developer.android.com/reference/']
}

apply from: rootProject.file('release.gradle')
